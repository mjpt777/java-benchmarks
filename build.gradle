/*
 * Copyright 2018-2021 Real Logic Limited.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

defaultTasks 'clean', 'build', 'shadowJar'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src/jmh/java'
        }
    }
}

dependencies {
    implementation 'org.agrona:agrona:1.14.0'
    implementation 'org.openjdk.jmh:jmh-core:1.33'
    annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.33'
}

jar.enabled = false

shadowJar {
    archiveFileName = 'benchmarks.jar'
    manifest.attributes('Main-Class': 'org.openjdk.jmh.Main')
}

task runBenchmarks(type: Exec) {
    commandLine 'java',
    '-jar', 'build/libs/benchmarks.jar',
    '.*Benchmark.*',
    '-w', '1s', '-r', '5s', '-wi', '3', '-i', '5', '-f', '5',
    '-jvmArgsAppend', '-XX:+UseParallelGC -Xms512m -Xmx512m -XX:BiasedLockingStartupDelay=0'
}

task runLatencyBenchmarks(type: Exec) {
    commandLine 'java',
    '-jar', 'build/libs/benchmarks.jar',
    '.*DatagramChannelLatencyBenchmark.*',
    '-w', '1s', '-r', '5s', '-wi', '3', '-i', '5', '-f', '5',
    '-jvmArgsAppend', '-XX:+UseParallelGC -Xms512m -Xmx512m -XX:BiasedLockingStartupDelay=0 -Djava.net.preferIPv4Stack=true'
}

task runProfilingBenchmarks(type: Exec) {
    commandLine 'java',
    '-jar', 'build/libs/benchmarks.jar',
    '.*MultiThreadedConnectedDatagramChannelBenchmark.*',
    '-w', '1s', '-r', '30s', '-wi', '5', '-i', '1', '-f', '1',
    '-jvmArgsAppend', '-XX:+UseParallelGC -Xms512m -Xmx512m -XX:BiasedLockingStartupDelay=0 -XX:+UnlockDiagnosticVMOptions -XX:+DebugNonSafepoints'
}

task runGcProfileBenchmarks(type: Exec) {
    commandLine 'java',
    '-jar', 'build/libs/benchmarks.jar',
    '.*Benchmark.*',
    '-w', '1s', '-r', '5s', '-wi', '3', '-i', '5', '-f', '1',
    '-prof', 'gc', '-jvmArgsAppend', '-XX:+UseParallelGC -Xms512m -Xmx512m -XX:BiasedLockingStartupDelay=0'
}

wrapper {
    gradleVersion = '7.2'
    distributionType = 'ALL'
}